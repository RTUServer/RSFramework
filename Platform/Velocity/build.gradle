import xyz.jpenilla.runtask.task.AbstractRun

plugins {
    id "xyz.jpenilla.run-velocity" version "2.3.1"
}

runVelocity {
    velocityVersion("3.4.0-SNAPSHOT")
}

dependencies {
    implementation project(":Library:ProtoWeaver:Common:API")
    implementation project(":Library:ProtoWeaver:Common:Core")
    implementation project(":Library:ProtoWeaver:Velocity:API")
    implementation project(":Library:ProtoWeaver:Velocity:Core")

    implementation "com.github.Carleslc.Simple-YAML:Simple-Yaml:1.8.4"

    compileOnly("com.velocitypowered:velocity-api:3.3.0-SNAPSHOT")
    annotationProcessor("com.velocitypowered:velocity-api:3.3.0-SNAPSHOT")
}

processResources {
    def props = [
            version: plugin_version,
            name   : plugin_name,
            author : plugin_author
    ]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("velocity-plugin.json") {
        expand props
    }
}

tasks.withType(AbstractRun.class).configureEach {
    javaLauncher = javaToolchains.launcherFor {
        vendor = JvmVendorSpec.JETBRAINS
        languageVersion = JavaLanguageVersion.of(21)
    }
    jvmArgs("-XX:+AllowEnhancedClassRedefinition")
}
