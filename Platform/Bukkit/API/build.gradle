plugins {
    id "maven-publish"
}

repositories {
    maven {
        url = "https://repo.oraxen.com/releases"
    }
    maven {
        url = "https://nexus.phoenixdevt.fr/repository/maven-public/"
    }
    maven {
        url = "https://repo.nexomc.com/snapshots/"
    }
    maven {
        url = "https://repo.auxilor.io/repository/maven-public/"
    }
    maven {
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        url = "https://repo.dmulloy2.net/repository/public/"
    }
}

dependencies {
    // API Build
    implementation project(":Library:LightDI")
    implementation project(":Library:ProtoWeaver:Common:API")
    implementation project(":Library:ProtoWeaver:Bukkit:API")
    implementation "com.alessiodp.libby:libby-bukkit:2.0.0-SNAPSHOT"

    compileOnly "com.github.LoneDev6:API-ItemsAdder:3.6.3-beta-14"

    compileOnly "io.th0rgal:oraxen:1.189.0"

    compileOnly "net.Indyuce:MMOItems-API:6.10.1-SNAPSHOT"
    compileOnly "io.lumine:MythicLib-dist:1.7.1-SNAPSHOT"

    compileOnly "com.nexomc:nexo:1.2.0-dev.11"

    compileOnly "com.willfp:EcoItems:5.62.0"
    compileOnly "com.willfp:eco:6.75.2"
    compileOnly "com.willfp:libreforge:4.64.1"

    compileOnly "me.clip:placeholderapi:2.11.6"
    compileOnly "com.github.MilkBowl:VaultAPI:1.7.1"
    compileOnly "com.comphenix.protocol:ProtocolLib:5.1.0"
    compileOnly "net.luckperms:api:5.4"

    compileOnly "org.reflections:reflections:0.10.2"

    implementation "com.github.Carleslc.Simple-YAML:Simple-Yaml:1.8.4"
    implementation "net.kyori:adventure-platform-bukkit:4.3.4"
    implementation "io.papermc:paperlib:1.0.8"
    implementation "de.tr7zw:item-nbt-api:2.14.1"
}

jar {
    finalizedBy(shadowJar)
}

shadowJar {
    setArchiveClassifier(null)
    setArchiveBaseName("api")
    setArchiveVersion(plugin_version)

    exclude "META-INF/**"
    exclude "classpath.index"
    relocate "org.simpleyaml", "kr.rtuserver.yaml"
    relocate "net.kyori.adventure.platform.bukkit", "kr.rtuserver.framework.bukkit.api.utility.kyori"
    relocate "io.papermc.lib", "kr.rtuserver.framework.bukkit.api.utility.paper"
    relocate "de.tr7zw.changeme.nbtapi", "kr.rtuserver.framework.bukkit.api.utility.nbtapi"


    doLast {
        copy {
            from archiveFile.get().asFile into file("$rootDir/builds/api")
        }
    }
}

publishing {
    repositories {
        maven {
            url = "https://repo.codemc.io/repository/rtuserver/"

            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }

    publications {
        api(MavenPublication) { publication ->
            groupId = "kr.rtuserver"
            artifactId = "framework-api"
            version = plugin_version

            project.shadow.component(publication)
        }
    }
}