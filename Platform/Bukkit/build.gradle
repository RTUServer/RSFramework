import xyz.jpenilla.runtask.task.AbstractRun

plugins {
    id "xyz.jpenilla.run-paper" version "2.3.1"
}

runServer {
    minecraftVersion(bukkit_version)
}

subprojects {
    dependencies {
        if (paper_plugin) compileOnly "io.papermc.paper:paper-api:${api_version}-R0.1-SNAPSHOT"
        else compileOnly "org.spigotmc:spigot-api:${api_version}-R0.1-SNAPSHOT"

        implementation project(":Library:ProtoWeaver:Bukkit:API")

        implementation "net.kyori:adventure-platform-bukkit:4.3.4"
        implementation "io.papermc:paperlib:1.0.8"
        implementation "de.tr7zw:item-nbt-api:2.14.1"

        compileOnly "com.alessiodp.libby:libby-bukkit:2.0.0-SNAPSHOT"

        compileOnly "org.mongodb:mongodb-driver-sync:5.4.0"
        compileOnly "com.mysql:mysql-connector-j:9.1.0"
        compileOnly "org.xerial:sqlite-jdbc:3.45.3.0"
        compileOnly "org.mariadb.jdbc:mariadb-java-client:3.3.3"
        compileOnly "org.postgresql:postgresql:42.7.3"
        compileOnly "com.zaxxer:HikariCP:5.1.0"
    }
}

// runServer Build
dependencies {
    implementation project("API")
    implementation project("Core")
    implementation project("Plugin")
    implementation "com.github.Carleslc.Simple-YAML:Simple-Yaml:1.8.4"
}

jar {
    finalizedBy(shadowJar)
}

shadowJar {
    setArchiveClassifier(null)
    setArchiveBaseName(plugin_name)
    setArchiveVersion(plugin_version)

    exclude "META-INF/**"
    exclude "classpath.index"
    relocate "org.simpleyaml", "kr.rtuserver.framework.yaml"
    relocate "net.kyori.adventure.platform.bukkit", "kr.rtuserver.framework.bukkit.kyori"
    relocate "de.tr7zw.changeme.nbtapi", "kr.rtuserver.framework.bukkit.api.nbt"
    relocate "io.papermc.lib", "kr.rtuserver.framework.bukkit.api.paper"
    relocate "org.apache.fury", "kr.rtuserver.protoweaver.fury"
}

tasks.withType(AbstractRun.class).configureEach {
    javaLauncher = javaToolchains.launcherFor {
        vendor = JvmVendorSpec.JETBRAINS
        languageVersion = JavaLanguageVersion.of(21)
    }
    jvmArgs("-XX:+AllowEnhancedClassRedefinition")
}
