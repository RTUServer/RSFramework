plugins {
    id "xyz.jpenilla.run-paper" version "2.3.1"
}

def projects = ["API", "Core"]

for (name in projects) project(":Framework:$name") {
    dependencies {
        implementation project(":Library:LightDI")

        compileOnly project(":Library:ProtoWeaver:Common:API")
        compileOnly project(":Library:ProtoWeaver:Bukkit:API")

        compileOnly "org.spigotmc:spigot-api:${api_version}-R0.1-SNAPSHOT"
        compileOnly "io.papermc.paper:paper-api:${api_version}-R0.1-SNAPSHOT"

        implementation "net.kyori:adventure-platform-bukkit:4.3.4"
        implementation "de.tr7zw:item-nbt-api:2.15.0"

        compileOnly "com.github.LoneDev6:API-ItemsAdder:3.6.3-beta-14"

        compileOnly "io.th0rgal:oraxen:1.189.0"

        compileOnly "net.Indyuce:MMOItems-API:6.10.1-SNAPSHOT"
        compileOnly "io.lumine:MythicLib-dist:1.7.1-SNAPSHOT"

        compileOnly "com.nexomc:nexo:1.2.0-dev.11"

        compileOnly "com.willfp:EcoItems:5.62.0"
        compileOnly "com.willfp:eco:6.75.2"
        compileOnly "com.willfp:libreforge:4.64.1"
    }
}

// runServer Build
dependencies {
    implementation project("API")
    implementation project("Core")
}

jar {
    finalizedBy(shadowJar)
}

shadowJar {
    setArchiveClassifier(null)
    setArchiveBaseName(plugin_name)
    setArchiveVersion(plugin_version)

    exclude "META-INF/**"
    exclude "classpath.index"
    relocate "org.simpleyaml", "kr.rtuserver.framework.yaml"
    relocate "net.kyori.adventure.platform.bukkit", "kr.rtuserver.framework.bukkit.kyori"
    relocate "de.tr7zw.changeme.nbtapi", "kr.rtuserver.framework.bukkit.api.nbt"
}

runServer {
    minecraftVersion(bukkit_version)
}

runPaper.folia.registerTask()