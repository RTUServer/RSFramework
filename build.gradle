plugins {
    id "java"
    id "maven-publish"
    id "io.freefair.lombok" version "8.11"
    id "com.gradleup.shadow" version "9.0.0-beta4"
    id "io.papermc.paperweight.userdev" version "2.0.0-beta.8" apply false
}

group = "kr.rtuserver"
version = plugin_version

allprojects {

    apply {
        plugin("java")
        plugin("io.freefair.lombok")
        plugin("com.gradleup.shadow")
    }

    repositories {
        mavenCentral()
        maven {
            name = "maven-snapshots"
            url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "AlessioDP"
            url = "https://repo.alessiodp.com/releases/"
        }
        maven {
            name = "spigotmc-repo"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            name = "papermc"
            url = "https://repo.papermc.io/repository/maven-public/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
        maven {
            name = "CodeMC"
            url = "https://repo.codemc.io/repository/maven-public/"
        }
        maven {
            url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
        }
        maven {
            url = "https://mvnrepository.com/artifact/org.projectlombok/lombok"
        }
        maven {
            url = "https://jitpack.io"
        }
        maven {
            url = "https://repo.oraxen.com/releases"
        }
        maven {
            url = "https://repo.dmulloy2.net/repository/public/"
        }
        maven {
            url = "https://repo.viaversion.com"
        }
        maven {
            url = "https://nexus.phoenixdevt.fr/repository/maven-public/"
        }
        maven {
            url = "https://repo.nexomc.com/snapshots/"
        }
    }

    dependencies {
        compileOnly "org.quartz-scheduler:quartz:2.5.0-rc1"

        compileOnly "net.kyori:adventure-text-minimessage:4.16.0"
        compileOnly "com.google.code.gson:gson:2.10.1"
        compileOnly "com.google.guava:guava:32.1.2-jre"
        compileOnly "org.apache.commons:commons-lang3:3.14.0"
        compileOnly "org.xerial.snappy:snappy-java:1.1.10.5"
        compileOnly "org.projectlombok:lombok:1.18.32"
        compileOnly "org.slf4j:slf4j-api:2.0.16"
    }

    java {
        def javaVersion = JavaVersion.toVersion(java_version as Integer)
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        if (JavaVersion.current() < javaVersion) toolchain.languageVersion = JavaLanguageVersion.of(java_version as Integer)
    }

    tasks.withType(JavaCompile).configureEach {
        options.release = java_version as Integer
    }
}

build {
    dependsOn(shadowJar)
}

dependencies {
    implementation project(":Platform:Bukkit")
    implementation project(":Platform:Bungee")
    implementation project(":Platform:Velocity")

    implementation "com.alessiodp.libby:libby-bukkit:2.0.0-SNAPSHOT"
    implementation "com.alessiodp.libby:libby-bungee:2.0.0-SNAPSHOT"
    implementation "com.alessiodp.libby:libby-velocity:2.0.0-SNAPSHOT"
    implementation "net.kyori:adventure-platform-bukkit:4.3.4"

    compileOnly "org.projectlombok:lombok:1.18.32"
    annotationProcessor "org.projectlombok:lombok:1.18.32"
}

shadowJar {
    file("$rootDir/builds").delete()

    setArchiveClassifier(null)
    setArchiveBaseName(plugin_name)
    setArchiveVersion(plugin_version)

    exclude "META-INF/**"
    exclude "classpath.index"
    relocate "net.kyori.adventure.platform.bukkit", "kr.rtuserver.framework.bukkit.api.utility.kyori"
    relocate "io.papermc.lib", "kr.rtuserver.framework.bukkit.api.utility.paper"
    relocate "de.tr7zw.changeme.nbtapi", "kr.rtuserver.framework.bukkit.api.utility.nbtapi"

    doLast {
        copy {
            from archiveFile.get().asFile into file("$rootDir/builds/plugin")
        }
    }
}

publishing {
    repositories {
        maven {
            url = "https://repo.codemc.io/repository/rtuserver/"

            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = "kr.rtuserver"
            artifactId = "framework"
            version = plugin_version

            from components.java

            pom {
                archivesBaseName = "rsframework-" + version + ".jar"

                name.set("RSFramework")
                description.set("Framework for RSPlugin")
                url.set("https://github.com/RTUServer/RSFramework")

                licenses {
                    license {
                        name.set("GNU GENERAL PUBLIC LICENSE")
                        url.set("https://www.gnu.org/licenses/gpl-3.0.de.html")
                    }
                }

                developers {
                    developer {
                        id.set("ipecter")
                        name.set("IPECTER")
                        email.set("ipectert@gmail.com")
                    }
                }
            }
        }
    }
}